#!/usr/bin/env perl
use warnings;
use strict;
use Nullroute::Lib;

sub clean {
	my ($root) = @_;

	_debug("cleaning '$root'");

	# recursively remove contents

	if (opendir(my $dh, $root)) {
		while (defined(my $name = readdir($dh))) {
			my $path = "$root/$name";
			_debug("- child '$path'");
			if ($name eq "." || $name eq "..") {
				;
			}
			elsif (-l $path) {
				;
			}
			elsif (-d $path) {
				clean($path);
			}
		}
		closedir($dh);
	} else {
		_err("cannot open '$root': $!");
	}

	# remove the directory itself

	if ($root eq ".") {
		;
	}
	elsif ($root =~ m|/\.git/refs$|) {
		_notice("skipping '$root' (required by Git)");
	}
	elsif (rmdir($root)) {
		_info("removed: $root");
	}
	elsif ($!{ENOTEMPTY}) {
		;
	}
	else {
		_err("cannot remove '$root': $!");
	}
}

sub undo {
	my @dirs;

	while (<STDIN>) {
		if (/.*?removed: (.+)$/) {
			push @dirs, $1;
		}
	}

	for (reverse @dirs) {
		if (mkdir($_)) {
			_info("created $_");
		} else {
			_err("cannot create '$_': $!");
		}
	}
}

if (@ARGV) {
	if ($ARGV[0] eq "--undo") {
		undo();
		exit !!$::errors;
	}
	clean($_) for @ARGV;
} else {
	clean(".");
}

exit !!$::errors;
